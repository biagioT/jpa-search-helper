plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'app.tozzi'
version = '2.1.1'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // JPA
    implementation 'jakarta.persistence:jakarta.persistence-api'

    // Validation
    implementation 'jakarta.validation:jakarta.validation-api'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-annotations'

    // Spring
    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework:spring-beans'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
}

publishing {
    publications {
        maven(MavenPublication) {
            group = 'app.tozzi'
            artifactId = 'jpa-search-helper'
            version = "2.1.1"
            from components.java
            pom {
                name = 'JPA Search Helper'
                description = 'Helper library for building advanced and dynamic queries using JPA in Spring'
                url = 'https://www.tozzi.app'

                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id = 'biagio.tozzi'
                        name = 'Biagio Placido Tozzi'
                        email = 'biagio.tozzi@gmail.com'
                        url = 'https://www.tozzi.app'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:biagioT/jpa-search-helper.git'
                    developerConnection = 'scm:git:ssh://github.com:biagioT/jpa-search-helper.git'
                    url = 'https://github.com/biagioT/jpa-search-helper/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            name "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USER")
                password = project.findProperty("sonatypePassword") ?: System.getenv("SONATYPE_TOKEN")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.allSource
}

bootJar {
    enabled = false
}
