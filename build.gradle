plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'app.tozzi'
version = '3.5.5'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // BOM
    implementation(platform("org.springframework.boot:spring-boot-dependencies:3.5.7"))

    // JPA
    implementation 'jakarta.persistence:jakarta.persistence-api'

    // Validation
    implementation 'jakarta.validation:jakarta.validation-api'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-annotations'

    // Spring
    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework:spring-beans'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.19.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = 'app.tozzi'
            artifactId = 'jpa-search-helper'
            version = "3.5.5"
            from components.java

            pom {
                name = 'JPA Search Helper'
                description = 'Library for building and running advanced and dynamic queries using JPA in Spring Boot.'
                url = 'https://www.tozzi.app'

                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id = 'biagio.tozzi'
                        name = 'Biagio Placido Tozzi'
                        email = 'biagio.tozzi@gmail.com'
                        url = 'https://www.tozzi.app'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:biagioT/jpa-search-helper.git'
                    developerConnection = 'scm:git:ssh://github.com:biagioT/jpa-search-helper.git'
                    url = 'https://github.com/biagioT/jpa-search-helper/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

jar {
    enabled = true
    archiveClassifier.set('')
}
